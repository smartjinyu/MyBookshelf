buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

def static getApiKey() {
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('fabric.properties')))
    return props['apiKey']
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.smartjinyu.mybookshelf"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 7
        versionName "1.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [fabric_api_key: getApiKey()]
        }
        debug {
            manifestPlaceholders = [fabric_api_key: getApiKey()]
        }
    }
    lintOptions{
        checkReleaseBuilds false
        abortOnError false
        warning 'InvalidPackage'
    }

    dataBinding {
        enabled = true
    }
}


dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'

    // base
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v13:25.3.1'
    compile 'com.android.support:palette-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'

    // ui
    compile 'com.github.clans:fab:1.6.4'
    compile 'com.afollestad.material-dialogs:core:0.9.4.2'
    compile 'com.klinkerapps:sliding-activity:1.4.4'
    compile('com.mikepenz:materialdrawer:5.9.0@aar') {
        transitive = true
    }

    // network
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile('com.squareup.retrofit2:converter-simplexml:2.2.0') {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }

    // others
    compile 'me.dm7.barcodescanner:zxing:1.9'
    compile 'com.nononsenseapps:filepicker:4.0.0'
    compile 'com.github.promeg:tinypinyin:2.0.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }
    compile project(':searchview')
    compile project(':AlipayZeroSdk')
    compile project(':compressor')
    compile files('libs/opencsv-3.9.jar')

    // di
    compile 'com.google.dagger:dagger:2.10'
    provided 'com.google.dagger:dagger-compiler:2.10'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    compile 'com.jakewharton:butterknife:8.5.1'
    apt 'com.jakewharton:butterknife-compiler:8.5.1'

}
