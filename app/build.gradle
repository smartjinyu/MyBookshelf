
buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'


repositories {
    maven { url 'https://maven.fabric.io/public' }
}


def static getFabricApiKey() {
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('apiKeys.properties')))
    return props['fabricApiKey']
}

def static getDoubanApiKey() {
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('apiKeys.properties')))
    byte[] data = props['doubanApiKey'].getBytes("UTF-8")
    data[0] = data[0] + 1
    data[1] = data[1] - 2 // just for obfuscation
    String base64 = Base64.getEncoder().encodeToString(data)
    return base64
}

android {
    compileSdkVersion project.CompileSdkVersion
    buildToolsVersion project.BuildToolsVersion
    defaultConfig {
        applicationId "com.smartjinyu.mybookshelf"
        minSdkVersion 21
        targetSdkVersion project.TargetSdkVersion
        versionCode 11
        versionName "1.6"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [fabric_api_key: getFabricApiKey(), douban_api_key: getDoubanApiKey()]
            buildConfigField 'String', "doubanApiKey", "\"" + getDoubanApiKey() + "\""
        }
        debug {
            manifestPlaceholders = [fabric_api_key: getFabricApiKey(), douban_api_key: getDoubanApiKey()]
            buildConfigField 'String', "doubanApiKey", "\"" + getDoubanApiKey() + "\""
        }
    }
    lintOptions {
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }


}


dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0-beta01'
    implementation 'androidx.palette:palette:1.0.0-beta01'
    testImplementation 'junit:junit:4.12'
    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'com.github.clans:fab:1.6.4' // discontinued project
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'
    implementation 'com.squareup.retrofit2:retrofit:2.7.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.2'
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0' // 3.3.0
    implementation 'com.nononsenseapps:filepicker:4.0.0' // replace it with built in document UI
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.klinkerapps:sliding-activity:1.5.2'
    implementation 'com.mikepenz:materialdrawer:6.1.2'
    implementation 'com.github.promeg:tinypinyin:2.0.3'
    // implementation project(':searchview')
    implementation 'com.lapism:searchview:3.8'
    implementation('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') { // replace it later
        transitive = true
    }
    // implementation project(':AlipayZeroSdk')
    implementation project(':compressor')
    // implementation 'id.zelory:compressor:2.1.0'
    implementation('com.squareup.retrofit2:converter-simplexml:2.2.0') {
        exclude group: 'xpp3', module: 'xpp3'
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
    }
    implementation files('libs/opencsv-3.9.jar')


}
